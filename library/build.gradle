apply plugin: "com.android.library"

apply from: "${rootDir}/gradle/android-common.gradle"

group = "com.okta.android"
version = "${rootProject.versionName}"

android {

    defaultConfig {
        project.archivesBaseName = "appauth-android"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    sourceSets {
        main {
            java {
                srcDir "src/main/java"
                srcDir "${rootDir.absolutePath}/app-auth/AppAuth-Android/library/java"
            }
        }
        test {
            manifest {
                srcFile "${rootDir.absolutePath}/library/src/test/AndroidManifest.xml"
            }
        }
        androidTest {
            manifest {
                srcFile "${rootDir.absolutePath}/library/src/test/AndroidManifest.xml"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


buildscript {
   repositories {
       jcenter()
   }
   dependencies {
       classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${rootProject.bintrayVersion}"
       classpath "com.github.dcendents:android-maven-gradle-plugin:${rootProject.gradlePluginVersion}"
   }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: "com.github.dcendents.android-maven"

dependencies {
    //used as a provider of manifest for app-auth
    //should be removed when singout functionality is merged
    implementation project(':appauthlibrary')
    implementation "com.squareup.okio:okio:${rootProject.okioVersion}"
    implementation "com.android.support:customtabs:${rootProject.browserVersion}"
    testImplementation "junit:junit:${rootProject.junitVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.mockitoVersion}"
    testImplementation "org.robolectric:robolectric:${rootProject.robolectricVersion}"
    testImplementation "com.squareup.assertj:assertj-android:${rootProject.assertjVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${rootProject.okhttpVersion}"
    testImplementation "com.squareup.okhttp3:okhttp:${rootProject.okhttpVersion}"
}

apply from: "${rootDir}/gradle/style.gradle"

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    configurations = ["archives"]
    pkg {
        repo = "com.okta.android"
        name = "okta-sdk-appauth-android"
        desc = "Okta Authentication using AppAuth"
        userOrg = "okta"
        licenses = ["Apache-2.0"]
        websiteUrl = "https://github.com/okta/okta-sdk-appauth-android"
        vcsUrl = "https://github.com/okta/okta-sdk-appauth-android.git"
        issueTrackerUrl = "https://github.com/okta/okta-sdk-appauth-android/issues"
        version {
            name = "${rootProject.versionName}"
            desc = "Okta Authentication using AppAuth"
            vcsTag = "${rootProject.versionName}"
        }
    }
}
